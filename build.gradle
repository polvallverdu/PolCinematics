plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    mavenLocal()

    maven { url 'https://jitpack.io' }
    maven {
        name = 'Sponge / Mixin'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }

    // modrinth
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Websocket dependencies
    implementation 'org.eclipse.jetty.websocket:websocket-jetty-api:10.0.14'
    implementation 'org.eclipse.jetty.websocket:websocket-jetty-server:10.0.14'

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Config stuff
    //modImplementation("com.electronwill.night-config:core:3.6.5")
    //modImplementation("com.electronwill.night-config:json:3.6.5")
    implementation 'com.google.code.gson:gson:2.9.0'

    // FancyVideo-API for video stuff
    modImplementation("com.github.polvallverdu.FancyVideo-API-PolCinematics:FancyVideo-API-fabric-1.19.4:c5ad45c61a")

    // Embedded Chromium (jcef)
    implementation 'me.friwi:jcefmaven:110.0.25'

    // Apache Commons
    // https://mvnrepository.com/artifact/org.apache.commons/commons-numbers-core
    implementation 'org.apache.commons:commons-numbers-core:1.1'
    // implementation 'org.apache.commons:commons-lang3:3.0'

    //////////// TESTING MODS ////////////
    modLocalRuntime "maven.modrinth:sodium:mc1.19.4-0.4.10"
    modLocalRuntime "maven.modrinth:lithium:mc1.19.4-0.11.1"
    modLocalRuntime "maven.modrinth:iris:1.5.2+1.19.4"
    modLocalRuntime "maven.modrinth:modmenu:6.1.0-rc.1"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

// TODO: Shadow FancyVideo-API (and maybe upload to repo)
